#include <esp32cam.h>
#include <WebServer.h>
#include <WiFi.h>
 
const char* WIFI_SSID = "jjj";
const char* WIFI_PASS = "12345678";
 
WebServer server(80);

const int lockPin = 2;
const int buzzPin = 13;
 
static auto loRes = esp32cam::Resolution::find(320, 240);
static auto midRes = esp32cam::Resolution::find(350, 530);
static auto hiRes = esp32cam::Resolution::find(800, 600);
void serveJpg()
{
  auto frame = esp32cam::capture();
  if (frame == nullptr) {
    Serial.println("CAPTURE FAIL");
    server.send(503, "", "");
    return;
  }
  Serial.printf("CAPTURE OK %dx%d %db\n", frame->getWidth(), frame->getHeight(),
                static_cast<int>(frame->size()));
 
  server.setContentLength(frame->size());
  server.send(200, "image/jpeg");
  WiFiClient client = server.client();
  frame->writeTo(client);
}

void handleLock1() {
  digitalWrite(lockPin, HIGH);
  server.send(200, "text/plain", "Lock set to 1");
  delay(3000);
  digitalWrite(lockPin, LOW);

}


void handleBuzz1() {
  digitalWrite(buzzPin, HIGH);
  server.send(200, "text/plain", "Buzzer set to 1");
  delay(1500);
  digitalWrite(buzzPin, LOW);
}


void handleJpgLo()
{
  if (!esp32cam::Camera.changeResolution(loRes)) {
    Serial.println("SET-LO-RES FAIL");
  }
  serveJpg();
}
 
void handleJpgHi()
{
  if (!esp32cam::Camera.changeResolution(hiRes)) {
    Serial.println("SET-HI-RES FAIL");
  }
  serveJpg();
}
 
void handleJpgMid()
{
  if (!esp32cam::Camera.changeResolution(midRes)) {
    Serial.println("SET-MID-RES FAIL");
  }
  serveJpg();
}
 
 
void setup(){
  pinMode(lockPin, OUTPUT);
  pinMode(buzzPin, OUTPUT);
  digitalWrite(lockPin, LOW);  
  
  Serial.begin(115200);
  Serial.println();
  {
    using namespace esp32cam;
    Config cfg;
    cfg.setPins(pins::AiThinker);
    cfg.setResolution(hiRes);
    cfg.setBufferCount(2);
    cfg.setJpeg(80);
 
    bool ok = Camera.begin(cfg);
    Serial.println(ok ? "CAMERA OK" : "CAMERA FAIL");
  }
  WiFi.persistent(false);
  WiFi.mode(WIFI_STA);
  WiFi.begin(WIFI_SSID, WIFI_PASS);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
  }
  Serial.print("http://");
  Serial.println(WiFi.localIP());
  Serial.println("  /cam-lo.jpg");
  Serial.println("  /cam-hi.jpg");
  Serial.println("  /cam-mid.jpg");
 
  server.on("/cam-lo.jpg", handleJpgLo);
  server.on("/cam-hi.jpg", handleJpgHi);
  server.on("/cam-mid.jpg", handleJpgMid);
  server.on("/lock/1", HTTP_GET, handleLock1);
  server.on("/buzz/1", HTTP_GET, handleBuzz1);

  server.begin();
}
 
void loop()
{
  server.handleClient();
}
